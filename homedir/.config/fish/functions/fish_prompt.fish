function fish_prompt --description 'Write out the prompt'
        set -l last_pipestatus $pipestatus
        set -lx __fish_last_status $status # Export for __fish_print_pipestatus.
        set -l normal (set_color normal)
        set -q fish_color_status
        or set -g fish_color_status red

        # Color the prompt differently when we're root
        set -l color_cwd $fish_color_cwd
        set -l suffix '>'
        if functions -q fish_is_root_user; and fish_is_root_user
                if set -q fish_color_cwd_root
                        set color_cwd $fish_color_cwd_root
                end
                set suffix '#'
        end

        # Write pipestatus
        # If the status was carried over (if no command is issued or if `set` leaves the status untouched), don't bold it.
        set -l bold_flag --bold
        set -q __fish_prompt_status_generation; or set -g __fish_prompt_status_generation $status_generation
        if test $__fish_prompt_status_generation = $status_generation
                set bold_flag
        end
        set __fish_prompt_status_generation $status_generation
        set -l status_color (set_color $fish_color_status)
        set -l statusb_color (set_color $bold_flag $fish_color_status)
        set -l prompt_status (__fish_print_pipestatus "[" "]" "|" "$status_color" "$statusb_color" $last_pipestatus)

        if [ -f /etc/os-release ]
            set distro (grep '^ID=' /etc/os-release | cut -d '=' -f2 | tr -d '"')
        end
        set icon '‚ûú'
        if [ -d /data/data/com.termux ]
            set icon 'ÔÑ†'
        end
        switch $distro
            case fedora
                set icon 'Ôåã'
            case arch
                set icon 'Û∞£á'
            case archarm
                set icon 'Û∞£á'
            case endeavour
                set icon 'Û∞£á'
            case nixos
                set icon 'Û±ÑÖ'
            case vanilla
                set icon 'Ôç¶'
            case debian
                set icon 'ÓùΩ'
            case ubuntu
                set icon 'ÓØâ'
            case ol
                set icon 'Û±Ñõ'
            case rhel
                set icon 'Û±Ñõ'
            case opensuse-tumbleweed
                set icon 'ÓΩ≠'
            case openwrt
                set icon 'Ôá´'
            case postmarketos
                set icon 'Ôç¥'
            case alpine
                set icon 'ÔåÄ'
        end
        if test -n "$SSH_CONNECTION"
            set icon "Û∞åó $icon"
        end
        if test -n "$container"
            set icon "üì¶$icon"
        end

        echo -n -s $icon " " (set_color $color_cwd) (prompt_pwd) $normal (fish_vcs_prompt) $normal " "$prompt_status $suffix " "
end

